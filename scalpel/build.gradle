import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
}

group 'lexfo'
version '0.0.1'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

def pyscalpelDir = '..' + File.separator + 'scripts' + File.separator + 'pyscalpel'

def venvPath = '..' + File.separator + '.venv'
def jepDir, jepJar

if (OperatingSystem.current().isWindows()) {
    jepDir = file(venvPath + File.separator + 'Lib' + File.separator + 'site-packages' + File.separator + 'jep').getAbsolutePath()
    jepJar = fileTree(dir: venvPath + File.separator + 'Lib', include: '**/jep/jep-*.jar').files.first().getAbsolutePath()
} else {
    jepDir = file(venvPath + File.separator + 'lib').listFiles()
        .find { it.name.startsWith('python3') }
        .listFiles().find { it.name.matches('site-packages') }
        .listFiles().find { it.name.matches('jep') }
        .getAbsolutePath()

    jepJar = fileTree(dir: venvPath + File.separator + 'lib', include: '**/jep/jep-*.jar').files.first().getAbsolutePath()
}

dependencies {
    compileOnly 'net.portswigger.burp.extensions:montoya-api:2023.3'
    
    implementation files(jepJar)
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.intellij:forms_rt:7.0.3'

    implementation 'org.jetbrains.jediterm:jediterm-pty:2.42'
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'log4j:log4j:1.2.17'
    implementation 'org.jetbrains.pty4j:pty4j:0.12.11'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.0-rc2'
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = 'exclude'
}

tasks.withType(Copy).all { duplicatesStrategy 'exclude' }

configurations {
    javadocImplementation.extendsFrom implementation
}

javadoc {
    source = sourceSets.main.allJava
    classpath += configurations.javadocImplementation
    options.links("https://portswigger.github.io/burp-extensions-montoya-api/javadoc/")
    options.memberLevel = JavadocMemberLevel.PRIVATE
    options.addStringOption('Xdoclint:none', '-quiet')
}
