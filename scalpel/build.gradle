plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'lexfo'
version '0.0.1'

repositories {
    mavenLocal()
    mavenCentral()
}

def venvPath = '../.venv'

def jepDir = file(venvPath + '/lib/').listFiles()
    .find { it.name.startsWith('python3') }
    .listFiles().find { it.name.matches('site-packages') }
    .listFiles().find { it.name.matches('jep') }
    .getAbsolutePath()

def jepJar = fileTree(dir: venvPath + '/lib', include: '**/jep/jep-*.jar').files.first().getAbsolutePath()

dependencies {
    compileOnly 'net.portswigger.burp.extensions:montoya-api:2023.1'
    
    // JEP installed from pip in a venv.
    implementation files(jepJar)
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.intellij:forms_rt:7.0.3'
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = 'exclude'
}

sourceSets {
    main {
        resources {
            srcDirs jepDir
        }
    }
}

tasks.withType(Copy).all { duplicatesStrategy 'exclude' }


configurations {
    javadocImplementation.extendsFrom implementation
}

javadoc {
    source = sourceSets.main.allJava
    classpath += configurations.javadocImplementation
    options.links("https://portswigger.github.io/burp-extensions-montoya-api/javadoc/")
    options.memberLevel = JavadocMemberLevel.PRIVATE
    options.addStringOption('Xdoclint:none', null)
}
