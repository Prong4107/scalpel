plugins {
    id 'java'
}

group 'lexfo'
version '0.0.1'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

def pyscalpelDir = '../scripts/pyscalpel'

def venvPath = '../.venv'

def jepDir = file(venvPath + '/lib/').listFiles()
    .find { it.name.startsWith('python3') }
    .listFiles().find { it.name.matches('site-packages') }
    .listFiles().find { it.name.matches('jep') }
    .getAbsolutePath()

def jepJar = fileTree(dir: venvPath + '/lib', include: '**/jep/jep-*.jar').files.first().getAbsolutePath()

dependencies {
    // Check the lastest version: https://github.com/PortSwigger/burp-extensions-montoya-api#gradle:~:text=2023.3%3C/version%3E%0A%3C/dependency%3E-,Gradle,-implementation%20%27net.portswigger
    compileOnly 'net.portswigger.burp.extensions:montoya-api:2023.3'
    
    // JEP installed from pip in a venv.
    implementation files(jepJar)
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.intellij:forms_rt:7.0.3'


    // Jediterm PTY library
    implementation 'org.jetbrains.jediterm:jediterm-pty:2.42'
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'log4j:log4j:1.2.17'
    implementation 'org.jetbrains.pty4j:pty4j:0.12.11'

}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = 'exclude'
    
}

sourceSets {
    main {
        resources {
            srcDirs jepDir
            exclude '**/*.pyc'
        }
    }
}

tasks.withType(Copy).all { duplicatesStrategy 'exclude' }


configurations {
    javadocImplementation.extendsFrom implementation
}

javadoc {
    source = sourceSets.main.allJava
    classpath += configurations.javadocImplementation
    options.links("https://portswigger.github.io/burp-extensions-montoya-api/javadoc/")
    options.memberLevel = JavadocMemberLevel.PRIVATE
    options.addStringOption('Xdoclint:none', '-quiet')
}
